//  Change Toolbar
// New in WinEdt 11: overwrite the local Toolbar.ini!

  PushTagsandRegisters;

  IfFileExists("%b\ConfigEx\Toolbar.ini",!"Relax;",!"JMP(!'doCopy');");

  // Compare it to the default ones on %B\ConfigEx ...
  ReadFile("%b\ConfigEx\Toolbar.ini",0);

  // Default?
  ReadFile("%B\ConfigEx\Toolbar.ini",1);
  IfStr("%!0","%!1",!"=",!"JMP(!'doCopy');");

  // Predefined?
  ReadFile("%b\ConfigEx\Toolbars\Toolbar1l.ini",1);
  IfStr("%!0","%!1",!"=",!"JMP(!'doCopy');");
  ReadFile("%b\ConfigEx\Toolbars\Toolbar2l.ini",1);
  IfStr("%!0","%!1",!"=",!"JMP(!'doCopy');");
  ReadFile("%b\ConfigEx\Toolbars\Toolbar2s.ini",1);
  IfStr("%!0","%!1",!"=",!"JMP(!'doCopy');");
  ReadFile("%B\ConfigEx\Toolbars\Toolbar5x.ini",1);
  IfStr("%!0","%!1",!"=",!"JMP(!'doCopy');");

  // Local Toolbar has been customized! Display a warning ...

  Prompt("Локальная версия файла Toolbar.ini уже существует.%\%\"+>
         "Заменить ее с помощью предопределенной (кириллической) конфигурации и отменить предыдущие изменения?",>
         1,3,!"Relax;",!"JMP(!'done');",!"JMP(!'done');");

:doCopy:: =======================================================================

  LetReg(1,"%b\ConfigEx\Toolbars\Toolbar%$(|TOOLBAR|);.ini");
  IfStr("%$(|TOOLBAR|);",!"",!"=",!|LetReg(1,"%b\ConfigEx\Toolbar.ini");|);

  CopyFile("%!1","%b\ConfigEx\Toolbar.ini",1,1);

  LoadConfig("ConfigEx\Toolbar.ini");

:done:: =========================================================================

  // Cleanup ...
  LetReg(0);
  LetReg(1);
  PopTagsandRegisters;

End;
