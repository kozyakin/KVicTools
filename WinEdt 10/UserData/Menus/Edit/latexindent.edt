// -*- ASCII:EDT -*-
// TeX: latexindent ...

  Requires(20110812); // Requires this build of WinEdt to work properly

  PushTagsandRegisters;
  Assign('ExitCode-OK','0'); // OK Exit Code
  LetReg(9,'latexindent'); // Execution Modes Compiler
  LetReg(8,'');         // Log File (or empty)
  LetReg(7,'');         // Output Format
  LetReg(6,'%p');       // Startup Folder

  // Create localSettings.yaml if it doesn't exists already
  IfFileExists(`%p\localSettings.yaml`,!`Relax`,>
    !`OpenOutput('%p\localSettings.yaml');>
      WrL('defaultIndent: "  "');>
      WrL('onlyOneBackUp: 1%\');>
      WrL('modifyLineBreaks:');>
      WrL('  condenseMultipleBlankLinesInto: 1');>
      WrL('  textWrapOptions:');>
      WrL('    columns: 72');>
      WrL('    comments:');>
      WrL('      wrap: 1');>
      WrL('    huge: overflow%\');>  
      CloseOutput;`);

  // Display a Dialog for Extra Switches:
  IfStr("%$('%!9-GUI');","1","=",>
    !|GetString("Switches: ","%!9 Switches","%$('%!9--Switches');");>
      IfOK(!`Assign('%!9--Switches','%!?');`,!`PopTagsandRegisters;Exit;`);|,>
    !|Release('%!9--Switches');|);
  // Build the Command Line ...
  LetReg(5, "%$('%!9-Executable'); %$('%!9-Switches'); %$('%!9--Switches'); %$('%!9-Parameters');");

  // Use Execution Modes to determine the flow of execution...
  Exe('%b\Exec\ExeCompiler.edt');
  PopTagsandRegisters;

End;
