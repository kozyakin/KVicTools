// -*- ASCII:EDT -*-
// TeX: TeXify ...

  Requires(20180507); // Requires this build of WinEdt to work properly

  PushTagsandRegisters;
  SetExitHandler('Exit_Handler');

//  LockEnvVar(!'LATEX');
  GetEnvVar(0,!'LATEX');
  Assign('TMPLATEX','%!0');
//  LockEnvVar(!'BIBTEX');
  GetEnvVar(0,!'BIBTEX');
  Assign('TMPBIBTEX','%!0');
//  LockEnvVar(!'MAKEINDEX');
  GetEnvVar(0,!'MAKEINDEX');
  Assign('TMPMAKEINDEX','%!0');
//  LockVar(!'Max-TeXify-Iterations');
  Assign(!'Tmp-Max-TeXify-Iterations','%$(!|Max-TeXify-Iterations|);');
//  LockVar(!'Quick-TeXify');
  Assign(!'Tmp-Quick-TeXify','%$(!|Quick-TeXify|);');


  JMP('Start');

:Exit_Handler:: =====================================================
// Restore the Execution Modes even if execution
// is canceled or goes badly wrong for some reason ...
  Assign('Compile-Flag'); // Not inside a (multiple conversion) compile...
  Assign("Pre-Process");
  IfStr("%$('Force-Focus');","1","<>",>
    !|IfStr("%$('External-Call');","1","<>",!"FocusConsole(-1);");|);
  IfStr("%$('External-Call');","1","<>",!"Exe('%b\Exec\%$(|Exec-Dir|);\ExeMode.edt');");
  PopTagsandRegisters;
//
  SetEnvVar(!'LATEX','%$(!|TMPLATEX|);')
  Release('TMPLATEX');
  SetEnvVar(!'BIBTEX','%$(!|TMPBIBTEX|);')
  Release('TMPBIBTEX');
  SetEnvVar(!'MAKEINDEX','%$(!|TMPMAKEINDEX|);')
  Release('TMPMAKEINDEX');
//
  Assign(!'Max-TeXify-Iterations','%$(!|Tmp-Max-TeXify-Iterations|);');
  Release('Tmp-Max-TeXify-Iterations');
  Assign(!'Quick-TeXify','%$(!|Tmp-Quick-TeXify|);');
  Release('Tmp-Quick-TeXify');

Return;

:Start:: ============================================================
  Assign('ExitCode-OK','0'); // OK Exit Code
  LetReg(9,'TeXify');   // Execution Modes Compiler
  LetReg(8,'log');      // Log File (or empty)
  LetReg(7,'dvi');      // Output Format
  LetReg(6,'%O');       // Startup Folder

  Exe('%b\Exec\TeX\BibTeXAuto.edt');

  IfStr("%$(!'TeX-PragmaCmd');",!"1",!"<>",!"JMP(!'Pragma_Done');");

  GetPreamble("\begin{document}",0,99);
  FindInString("%!0",!'<%@{ }!Max@{ }iterations@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(1,"%#0");',!'LetReg(1)');
  FindInString("%!1",!'\#~{?}{0|1|2|3|4|5}~{?}',-1,-1,1);
  IfOK(!"Assign(!'Max-TeXify-Iterations','%!1');");
  FindInString("%!0",!'<%@{ }!Quick@{ }TeXify@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(1,"%#0");',!'LetReg(1)');
  FindInString("%!1",!'\#~{?}{0|1}~{?}',-1,-1,1);
  IfOK(!"Assign(!'Quick-TeXify','%!1');");
  FindInString("%!0",!'<%@{ }!TeXify@{ }engine@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(1,"%#0");',!'LetReg(1)');
  IfIStr("%!1",!"latex",!"=",!"SetEnvVar(!'LATEX','%$(!|LaTeX-Executable|);')");
  IfIStr("%!1",!"tex",!"=",!"SetEnvVar(!'LATEX','%$(!|TeX-Executable|);')");
  IfIStr("%!1",!"amstex",!"=",!"SetEnvVar(!'LATEX','%$(!|AMSTeX-Executable|);')");
  FindInString("%!0",!'<%@{ }!BIB @{ }program@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(1,"%#0");',!'LetReg(1);');
  FindInString("%!1",!'\#~{?}{bibtex8|bibtex|biber}~{?}',-1,-1,1);
  IfOK(!'FormatString("%!1",1,!"lowercase");',!'LetReg(1);');
  IfStr("%!1",!"",!"<>",!'Assign(!"BibTeX-Executable","%!1.exe");');
  FindInString("%!0",!'<%@{ }!BIB @{ }exe@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"BibTeX-Executable","%#0");');
  FindInString("%!0",!'<%@{ }!BIB @{ }opt@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"BibTeX-Switches","%#0");');
  FindInString("%!0",!'<%@{ }!BIB @{ }par@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"BibTeX-Parameters","%#0");');
  FindInString("%!0",!'<%@{ }!IND @{ }exe@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeIndex-Executable","%#0");');
  FindInString("%!0",!'<%@{ }!IND @{ }opt@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeIndex-Switches","%#0");');
  FindInString("%!0",!'<%@{ }!IND @{ }par@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeIndex-Parameters","%#0");');
  FindInString("%!0",!'<%@{ }!GLS @{ }exe@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeGlossary-Executable","%#0");');
  FindInString("%!0",!'<%@{ }!GLS @{ }opt@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeGlossary-Switches","%#0");');
  FindInString("%!0",!'<%@{ }!GLS @{ }par@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"MakeGlossary-Parameters","%#0");');
  FindInString("%!0",!'<%@{ }!TeXify @{ }exe@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"TeXify-Executable","%#0");');
  FindInString("%!0",!'<%@{ }!TeXify @{ }opt@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"TeXify-Switches","%#0");');
  FindInString("%!0",!'<%@{ }!TeXify @{ }par@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign(!"TeXify-Parameters","%#0");');

:Pragma_Done:: ======================================================

  // MiKTeX's texify.exe?
  IfIStr("%$('%!9-Executable');",!"texify.exe",!"=",!"Assign(!'TeX-Opt',!'--tex-option=');",!"Assign(!'TeX-Opt');");

  // Display a Dialog for Extra Switches:
  IfStr("%$('%!9-GUI');","1","=",>
    !|GetString("Switches: ","%!9 Switches","%$('%!9--Switches');");>
      IfOK(!`Assign('%!9--Switches','%!?');`,!`Exit;`);|,>
    !|Release('%!9--Switches');|);
  LetReg(5,@@"%$('%!9-Switches');");
  IfIStr(@@"%$('%!9-Executable');","dvilualatex.exe","=",>
    !"SubstituteInString('%!5','--src','',10,5);Assign('%!9-Switches','%!5');");
  LetReg(3, "%$('%!9-Executable'); %$('%!9-Switches');");
  IfIStr("%$('%!9-Executable');",!"texify.exe",!"<>",!"JMP('no_Batch');");
  IfStr("%$('%!9-No_Interaction');","1",!"=",!|LetReg(3,'%!3 --batch');|);
  SetEnvVar("BIBTEX","%$('BibTeX-Executable');");
  SetEnvVar("MAKEINDEX","%$('MakeIndex-Executable');");
:no_Batch:: =========================================================
  // Non-Stop Mode?
  IfStr("%$('%!9-No_Interaction');","1",!"=",>
    !|LetReg(4,'%$("TeX-Opt");--interaction=nonstopmode');|,>
    !|LetReg(4,'%$("TeX-Opt");--interaction=errorstopmode');|);
  // C Style Errors?
  IfStr("%$('%!9-C-style-errors');","1",!"=",!|LetReg(4,'%!4 %$("TeX-Opt");--file-line-error');|);
  // Aux Directory?
  IfStr("%$('TeXOut-AutoIncDir');","1","<>",!"JMP('no_IncDir');");
  IfStr('%P','',!'=',!"JMP('no_IncDir');");
  IfStr('%O','%P',!'=',!"JMP('no_IncDir');");
  LetReg(4,'%!4 --include-directory="%%P"');
:no_IncDir:: ========================================================
  // TeX Sync?
  Assign('Sync');
  IfStr('%0',!'--src',!'<>',!'JMP("no_Sync");');
  IfStr('%$("PDF-SyncTeX");',!'1',!'<>',!'JMP("no_Sync");');
  IfStr('%$("PDF-Sync");',!'',!'=',!'JMP("no_Sync");');
  Assign('Sync','%$("TeX-Opt");%$("PDF-Sync");');
:no_Sync:: ==========================================================
  IfStr("%$(!'TeX-PragmaCmd');",!"1",!"<>",!"JMP(!'no_Pragma');");
  FindInString("%!0",!'<%@{ }!TeXify @{ }ext@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign("%!9--Switches","%$(`%!9--Switches`); %#0");');
  // Command Line overwrites the executable, switches, and parameters!
  FindInString("%!0",!'<%@{ }!TeXify @{ }cmd@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(5,"%#0");JMP(!"Execute");');
:no_Pragma:==========================================================

  // Build the Command Line ...
  LetReg(5, "%!3 %!4 %$('Sync'); %$('%!9--Switches'); %$('%!9-Parameters');");

:Execute:: ==========================================================
  // Clean the files
  IfFileExists("%O\%N.blg",!"DeleteFile('%O\%N.blg');");
  IfFileExists("%O\%N.ilg",!"DeleteFile('%O\%N.ilg');");
  IfFileExists("%O\%N.glg",!"DeleteFile('%O\%N.glg');");
  IfFileExists("%O\%N.nlg",!"DeleteFile('%O\%N.nlg');");
  IfStr("%$('%!9-Clean_Build');","1","=",>
    !|IfFileExists("%O\%N.bbl",!"DeleteFile('%O\%N.bbl');");>
      IfFileExists("%O\%N.bcf",!"DeleteFile('%O\%N.bcf');");>
      IfFileExists("%O\%N.ind",!"DeleteFile('%O\%N.ind');");>
      IfFileExists("%O\%N.idx",!"DeleteFile('%O\%N.idx');");>
      IfFileExists("%O\%N.gls",!"DeleteFile('%O\%N.gls');");>
      IfFileExists("%O\%N.glo",!"DeleteFile('%O\%N.glo');");>
      IfFileExists("%O\%N.nls",!"DeleteFile('%O\%N.nls');");>
      IfFileExists("%O\%N.nlo",!"DeleteFile('%O\%N.nlo');");>
      IfFileExists("%O\%N.toc",!"DeleteFile('%O\%N.toc');");>
      IfFileExists("%O\%N.lof",!"DeleteFile('%O\%N.lof');");>
      IfFileExists("%O\%N.lot",!"DeleteFile('%O\%N.lot');");>
      IfFileExists("%O\%N.aux",!"DeleteFile('%O\%N.aux');");>
      IfFileExists("%O\%N.out",!"DeleteFile('%O\%N.out');");>
     |);
  UpdateStatus;

  IfStr("%$('%!9-WinEdt_Console');",'1','<>',!'JMP(!"Windows_Console");');

:WinEdt_Console:: ===================================================
  // Running from TeXify / Compile Macro?
  IfStr("%$('Compile-Flag');",'','=',>
   !|>
      IfStr("%$('%!9-Hidden_Window');",'1','=',!"ResetConsole;",!"ResetConsole;ShowConsole(1);");>
      IfStr("%$('%!9-Background_Window');",'1','=',!"FocusConsole(-1);",!"FocusConsole(1);");>
    |);
  JMP('Console_OK');

:Windows_Console:: ==================================================
  IfStr("%$('Compile-Flag');",'','=',>
    !|IfStr("%$('%!9-STD_IO');","1","=",!"ResetConsole;");|);

:Console_OK:: =======================================================
  Assign('Report-Shown','');
  Assign('Force-Focus','');
  Assign("TeXify-Iterations","%$('Max-TeXify-Iterations');");

:TeXify_Iterate:: ===================================================
  LetReg(3,"%$('TeXify-Iterations');");
  IfStr("%$('Pre-Process');","","=",!"Assign('Pre-Process','TeXify');");
  //Assign("Pre-Process","PDFTeXify");
  IfStr("%$(!'Quick-TeXify');",!"1",!"=",>
    !`IfNum("%$(!'Max-TeXify-Iterations');","%!3",!">",>
      !|Exe('%b\Exec\TeX\BibTeXify.edt');|);`,>
    !`IfNum("%$(!'Max-TeXify-Iterations');",!"0",!">",>
      !|Exe('%b\Exec\TeX\BibTeXify.edt');|);`);
  Assign("Pre-Process");
  // Use Execution Modes to determine the flow of execution...
  Assign(!"Compile-Flag",!"3");
  Exe('%b\Exec\ExeCompiler.edt');
  IfStr("%$('Compile Status');",!"OK",!"<>",>
    !"IfStr(`%$('Compile Status');`,`Output`,`<>`,!`JMP('TeXify_Done');`);");
  LetReg(3,"%$('TeXify-Iterations');");
  LetRegNum(3,"%!3-1");
  Assign("TeXify-Iterations","%!3");
  IfNum("%!3",!"0",!">",!"JMP('TeXify_Iterate');");

:TeXify_Done:: ======================================================
  IfStr("%$('Report-Shown');",!"1",!"<>",>
    !"IfStr(`%$('External-Call');`,`1`,`<>`,>
        !`IfStr(|%$('Compile Status');|,|Run Error|,|<>|,>
            !|IfStr(*%$('%!9-Report');*,*1*,*=*,!*Exe('%b\Exec\ShowReport.edt');*);|);`);");

End;
