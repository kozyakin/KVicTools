// !Mode:: "EDT:UTF-8"
// -*- EDT:UTF-8 -*-
// changeEnv.edt
// changes the current environment
// (schlicht 20010705)
// modified by V.Kozyakin 20130403
//=========================================================================


//=========================================================================
// There are two ways to invoke this macro:
// 1. by an Active String, i.e. by typing ">" after the environment commands
//      "\begin{environment}"
//    and / or
//      "\end{environment}";
//    or "$$", or "\[";
// 2. manually (resp. via a shortcut) from anywhere inside the environment.
//
//  To define an Active String,
//    * Go to Options | Settings | Active Strings;
//    * Right-click in the list and insert a new item:
//         "\begin{?}>"
//      and / or
//         "$$>"
//      and / or
//         "\[>"
//      and maybe also "\end{?}>" etc.
//    * Define the Action:
//          Exe("%B\Macros\LaTeX\ChangeEnvC.edt");
//    * Check the box in "Action" and define "TeX" as "On Type".
//
// You can add new environments to the list using the prompt.
// If it's a "normal" environment (e.g. "\begin{eqnarray}" ... "\end{eqnarray}"),
// you should specify the name of the environment ("eqnarray") only.
// Otherwise you have to specify both parts of the environment separated by "^",
// f.i. "\[^\]".
//
// Note that the macro relies on WinEdt's finding the matching delimiter,
// as defined in Options | Settings | Delimiters.
//
// The list of environments is defined in "ChangeEnvC.cfg". This file
// can be put in your local directory %b\Macros\LaTeX (e.g.
// "C:\Documents and Settings\<you>\Application Data\WinEdt\Macros\LaTeX").
////=============================================================================
Requires(20020323);
// If you're using an earlier version of WinEdt than 5.3, delete the above line.
// However, I cannot guarantee, that the macro works reliably.
// Better, get the latest version of WinEdt, available from <www.winedt.com>.
////=============================================================================


// Search for the configuration file in %b first,
// else take the global configuration file in %B.
Assign("ConfFile","Macros\LaTeX\ChangeEnvC.cfg");
IfFileExists("%b\%$(|ConfFile|);",>
  !"Assign('ConfFile','%b\%$(|ConfFile|);');",>
  !"IfFileExists('%B\%$(|ConfFile|);',>
      !'Assign(''ConfFile'',''%B\%$(|ConfFile|);'');',>
      !'Assign(''ConfFile'',''Prompt(|Файл ""%$(*ConfFile*);"" не найден!|,3,1); Exit;'');'>
    );">
);>

Assign("escapeSpecials",!`>
   Assign("tmp0","%!0");>
   Assign("tmp1","%!1");>
   LetReg(1,"%$('escapeString');");>
   LetRegNum(0,-2);>
   Loop(!"SetOK(1);>
      FindInString('%!1','[\\\+\@\{\}\[\]\$\*\^\|\~\<\>\?\.]',0,0,1001,%!0+2);>
      IfOK(!'InsertByIndex(1,''%!1'',''\'',%!0);','Stop;');>
   ");>
   Assign("escapedString","%!1");>
   LetReg(0,"%$('tmp0');");>
   LetReg(1,"%$('tmp1');");>
`);

LetReg(8,"");
Interface("Изменить окружение");
Exe("%$('ConfFile');");

Item("","Relax;");
Item(`Другое окружение`,>
  !`LetReg(9,"");>
    EnterReg(9,"Изменить окружение на следующее:","Изменить окружение");>
    Prompt("Добавить %\   ""%!9""%\к списку окружений?",1,3,>
      !"ReadFile('%$(|ConfFile|);',3);>
        SetOK(1);>
        FindInString('%!3','Item(""%!9"",',4,5,10);>
        IfOK('Relax;',>
         !'SetOK(1);>
           FindInString(''%!3'',''/%_/ End of List of Environments>'',4,5,0011,0);>
           IfOK(!''>
              SetOK(1);>
              FindInString(|%!9|,|\(8*\)\^\(9*\)|,5,6,1);>
              IfOK(!|SetOK(1);>
                 LetReg(7,||%!9||);>
                 GetTag(8,8);>
                 GetTag(9,9);>
                 FindInString(||%!3||,||Assign(""changeEnv_findString"",""*"");||,5,6,1);>
                 IfOK(!||Assign(*escapeString*,*%!8*);>
                    Do(*%$(:escapeSpecials:);*);>
                    InsertByIndex(3,*%!3*,*||||{%$(:escapedString:);}*,%!6-2);||>
                 );>
                 LetReg(7,||Item(""%!7"",""LetReg(8,''''%!8'''');LetReg(9,''''%!9'''');"");%\||);>
                |,!|>
                 LetReg(7,||Item(""%!9"",""LetReg(9,''''%!9'''');"");%\||);|>
              );>
              InsertByIndex(3,|%!3|,|%!7|,%!4);>
              OpenOutput(|%$(*ConfFile*);|);>
              WrS(|%!3|);>
              CloseOutput;''>
           );'>
        );>
       ",!">
        SetOK(1);>
        FindInString('%!9','\(8*\)\^\(9*\)',5,6,1);>
        IfOK(!'>
           GetTag(8,8);>
           GetTag(9,9);'>
        );">
    );`>
);

Item("Удалить окружение",>
  !`SetTracking(0);>
    GetSel(0,2);>
    GetLength(8,"%!2");>
    Repeat(%!8,"SelCharLeft;");>
    Delete;>
    Goto(1);>
    GetLength(8,"%!1");>
    Repeat(%!8,"SelCharRight;");>
    Delete;>
    RestoreFind;>
    Goto(0);>
    SetTracking(1);>
    Refresh(1);>
    EndGroup;>
    Exit;>
`);

Item(`Удалить окружение из списка`,>
  !`Interface("Удалить окружение из списка");>
    Exe("%$('ConfFile');");>
    ShowList(1,1,!"Do('%?');","Goto(0);RestoreFind; Exit;");>
    Assign("escapeString","%!9");>
    Do("%$('escapeSpecials');");>
    LetReg(9,"%$('escapedString');");>
    ReadFile("%$('ConfFile');",0);>
    SetOK(1);>
    IfStr("%!8","","=",!"LetReg(8,'Item(""%!9"",@{ }""LetReg(9,''%!9'');"");>');",>
      !"Assign('escapeString','%!8');>
        Do('%$(|escapeSpecials|);');>
        LetReg(8,'Item(""%$(|escapedString|);\^%!9"",@{ }""LetReg(8,''%$(|escapedString|);'');@{ }LetReg(9,''%!9'');"");>');>
        GetLength(1,'%$(|escapedString|);');>
        Assign('escapeString','%$(|escapedString|);');>
        Do('%$(|escapeSpecials|);');>
        SetOK(1);>
        FindInString('%!0','Assign(""changeEnv_findString"",""*\|\{%$(|escapedString|);\}',2,2,1);>
        IfOK(!'ReplaceInString(''%!0'','''',%!2-%!1-2,%!2,0,0);');">
    );>
    SetOK(1);>
    FindInString("%!0","%!8",1,2,11);>
    IfOK(!">
       ReplaceInString('%!0','',%!1,%!2,0,0);>
       OpenOutput('%$(|ConfFile|);');>
       WrS('%!0');>
       CloseOutput;">
    );>
    RestoreFind;>
    Goto(0);>
    SetTracking(1);>
    Refresh;>
    EndGroup;>
    Exit;>
`);



// Now comes the real thing.

LetRegNum(0,%l*1000+%c);
SetTracking(0);
Mark(0);

// Anything preselected? Then give the macro
// a chance to find the environment again:
IfSel(0,">","CharRight;");

// Called by an Active String?
SelCharLeft;
IfStr("%S",">","=",>
  !`FindInString("%L","{", 1,2, 1110, %c-2);>
    FindInString("%L","\begin{", 1,3, 1110, %c-2);>
    IfNum(%!2,%!3,"<>","Goto(0);SetTracking(1);Exit;");`);

IfStr("%S",">","=","Delete;","CharRight(7);");

ResetGroup;
BeginGroup;
SaveFind;
SetFindTextSelect(1);
SetNotFoundNotify(1);
SetSearchSelected;
SetSearchCurrentDoc;
SetSearchCyclic(0);
SetSearchEntire(0);
SetSearchForward(0);
SetRegEx(1);
SetSearchCaseSensitive(1);
SetFindStr("%$('changeEnv_findString');");

Loop(!`>
   IfFound(>
     !"Mark(1);>
       GetSel(0,1);>
       MatchDelimiter(0,0,1,0);>
       IfOK('','Prompt(|Предыдущее предупреждение обычно появляется |+>
       |из-за нарушения синтаксиса команд LaTeX.%\%\|+>
       |Проверьте парность всех скобок в окружении!|,0,1,|LetReg(1,||Error||);|);>
       Goto(0);RestoreFind; Exit;');>
       IfNum(%!3,%l,'=','Mark(1);MatchDelimiter(0,0,1,1);IfOK('''',''Stop;'');');>
       LetRegNum(3,%l);>
       IfNum(%l*1000+%c+1,%!0,'>=',>
        !'SetTracking(1);>
          Refresh(1);>
          ShowList(1,1,!''Do(|%?|)'',''Goto(0);RestoreFind; Exit;'');>
          IfStr(''%!9'','''',''='',''Goto(0);RestoreFind; Exit;'');>
          SetTracking(0);>
          GetSel(0,2);>
          GetLength(7,''%!2'');>
          Repeat(%!7,''SelCharLeft;'');>
          IfStr(''%!8'','''',''='',''LetReg(8,|\begin{%!9}|);LetReg(9,|\end{%!9}|);'');>
          InsText(''%!9'');>
          Goto(1);>
          GetLength(7,''%!1'');>
          Repeat(%!7,''SelCharRight;'');>
          InsText(''%!8'');>
          Stop;',>
        !'MatchDelimiter(0,0,1,1);'>
       );>
       ",>
     "Stop;">
   );>
`);

RestoreFind;
GotoLin(%!0/1000);
SetTracking(1);
Refresh;
EndGroup;
End;


//-----------------------------------------------------------------------
  R Schlicht   w.m.l@gmx.net
  $Id: changeEnv.edt,v 1.6 2002-08-08 19:56:16+02 schlicht Exp schlicht $
