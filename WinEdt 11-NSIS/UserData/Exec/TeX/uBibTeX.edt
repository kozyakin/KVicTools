// -*- ASCII:EDT -*-
// TeX: BibTeX ...

  Requires(20211103); // Requires this build of WinEdt to work properly

  PushTagsandRegisters;
  SetExitHandler('Exit_Handler');

  Assign('ExitCode-OK','0'); // OK Exit Code
  LetReg(9,'BibTeXu');   // Execution Modes Compiler
  LetReg(8,'blg');      // Log File (or empty)
  LetReg(7,'bbl');      // Output Format
  LetReg(6,'%O');       // Startup Folder

  LockVar('%!9-Executable');
  LockVar('%!9-Switches');
  LockVar('%!9-Parameters');

  // Display a Dialog for Extra Switches:
  IfStr("%$('%!9-GUI');","1","=",>
    !|GetString("Switches: ","%!9 Switches","%$('%!9--Switches');");>
      IfOK(!`Assign('%!9--Switches','%!?');`,!`Exit;`);|,>
    !|Release('%!9--Switches');|);

  JMP(!'Start');

:Exit_Handler:: =====================================================
  PopTagsandRegisters;
  UpdateStatus;
Return;

:Start:: ============================================================
  IfFileExists('%O\%N.aux',!'Relax;',!"JMP(!'Run');");

  Exe('%b\Exec\TeX\BibTeXAuto.edt');

  IfStr("%$(!'TeX-PragmaCmd');",!"1",!"<>",!"JMP(!'Run');");
  GetPreamble("\begin{document}",0,99);
  // Parse for the executable (bibtex, bibtex8, or biber)
  FindInString("%!0",!'<%@{ }!BIB @{ }program@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(1,"%#0");',!'LetReg(1);');
  FindInString("%!1",!'\#~{?}{bibtex8|bibtex|biber}~{?}',-1,-1,1);
  IfOK(!'FormatString("%!1",1,!"lowercase");',!'LetReg(1);');
  IfStr("%!1",!"",!"<>",!'Assign(!"BibTeX-Executable","%!1.exe");');
  FindInString("%!0",!'<%@{ }!BIB @{ }exe@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign("%!9-Executable","%#0");');
  FindInString("%!0",!'<%@{ }!BIB @{ }opt@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign("%!9-Switches","%#0");');
  FindInString("%!0",!'<%@{ }!BIB @{ }par@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign("%!9-Parameters","%#0");');
  FindInString("%!0",!'<%@{ }!BIB @{ }ext@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'Assign("%!9--Switches","%$(`%!9--Switches`); %#0");');
  // Command Line overwrites the executable, switches, and parameters!
  FindInString("%!0",!'<%@{ }!BIB @{ }cmd@{ }=@{ }\(*\)@{ }>',-1,-1,1);
  IfOK(!'LetReg(5,"%#0");JMP(!"Exe");');

:Run:: ==========================================================================

  // Aux Directory?
  LetReg(4);
  IfStr("%$('TeXOut-AutoIncDir');","1","<>",!"JMP('no_IncDir');");
  IfStr('%P','',!'=',!"JMP('no_IncDir');");
  IfStr('%O','%P',!'=',!"JMP('no_IncDir');");
  IfStr("%$('%!9-Executable');","biber.exe",!'=',!|LetReg(4,'--input-directory="%%P"');|);
  IfStr("%$('TeX System');","MiKTeX","<>",!"JMP('no_IncDir');");
  IfStr("%$('%!9-Executable');","bibtexu.exe",!'=',!|LetReg(4,'--include-directory="%%P"');|);

:no_IncDir:: =====================================================================

  // Build the Command Line ...
  LetReg(5, "%$('%!9-Executable'); %$('%!9-Switches'); %!4 %$('%!9--Switches'); %$('%!9-Parameters');");

:Exe:: ==========================================================================

  // Use Execution Modes to determine the flow of execution...
  Exe('%b\Exec\ExeCompiler.edt');

End;
